/**
 * Maven Publish Helper
 *
 * Requires Android Gradle plugin 3.6.0 or higher (available since Android Studio 3.6).
 * See also: https://developer.android.com/studio/build/maven-publish-plugin
 *
 * @Author Robert PÃ¶sel
 * @Version 1.5
 * @Date 3.3.2020
 */

apply plugin: 'maven-publish'

task androidJavadocs(type: Javadoc) {
	source = android.sourceSets.main.java.srcDirs
	classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
	android.libraryVariants.all { variant ->
		if (variant.name.endsWith('Release')) {
			owner.classpath += variant.javaCompileProvider.get().classpath
		}
	}
	exclude '**/R.html', '**/R.*.html', '**/index.html'
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
	archiveClassifier.set('javadoc')
	from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
	archiveClassifier.set('sources')
	from android.sourceSets.main.java.srcDirs
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
	publishing {
		publications {
			android.libraryVariants.all { variant ->
				// Only consider release build variants.
				if (variant.buildType.name != "release") {
					return
				}

				"${variant.flavorName}"(MavenPublication) {
					// Applies the component for the build variant.
					from components.findByName(variant.name)

					// Adds javadocs and sources as separate jars.
					artifact androidJavadocsJar
					artifact androidSourcesJar

					groupId 'cz.adaptech'
					version android.defaultConfig.versionName
					artifactId 'tesseract4android'

					// Append flavor name to the version (unless it's "standard" flavor)
					if (variant.flavorName != "standard") {
						artifactId = "${artifactId}-${variant.flavorName}"
					}
				}
			}
		}
	}
}
