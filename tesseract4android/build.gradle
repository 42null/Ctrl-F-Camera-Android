plugins {
	id 'com.android.library'
	id 'maven-publish'
}

ext {
	libraryVersion = '4.2.0-dev'
}

android {
	namespace 'cz.adaptech.android.tesseract4android'
	compileSdkVersion 33
	ndkVersion "25.1.8937393"
	defaultConfig {
		minSdkVersion 16
		targetSdkVersion 28
		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		externalNativeBuild {
			cmake {
				// Specifies which native libraries or executables to build and package.
				// TODO: Include eyes-two in some build flavor of the library?
				//targets "jpeg", "png", "leptonica", "tesseract"
			}
		}
		ndk {
			// Specify the ABI configurations that Gradle should build and package.
			// By default it compiles all available ABIs.
			//abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
		}
	}
	externalNativeBuild {
		cmake {
			version '3.22.1'
		}
	}
	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}
	flavorDimensions "parallelization"
	productFlavors {
		standard {
		}
		openmp {
			versionNameSuffix "-openmp"
			externalNativeBuild {
				cmake {
					cFlags "-fopenmp"
					cppFlags "-fopenmp"
				}
			}
		}
	}
	publishing {
		singleVariant("standardRelease") {
			withSourcesJar()
			withJavadocJar()
		}
		singleVariant("openmpRelease") {
			withSourcesJar()
			withJavadocJar()
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_11
		targetCompatibility JavaVersion.VERSION_11
	}
	externalNativeBuild {
		cmake {
			path "src/main/cpp/CMakeLists.txt"
		}
	}
}

dependencies {
	implementation fileTree(include: ['*.jar'], dir: 'libs')
	implementation 'androidx.annotation:annotation:1.5.0'
	testImplementation 'junit:junit:4.13.2'
	androidTestImplementation 'androidx.test:runner:1.4.0'
	androidTestImplementation 'androidx.test:rules:1.4.0'
	androidTestImplementation 'androidx.test.ext:junit:1.1.3'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

afterEvaluate {
	publishing {
		publications {
			standard(MavenPublication) {
				from components.findByName('standardRelease')

				groupId 'cz.adaptech'
				artifactId 'tesseract4android'
				version project.ext.libraryVersion
			}
			openmp(MavenPublication) {
				from components.findByName('openmpRelease')

				groupId 'cz.adaptech'
				artifactId 'tesseract4android-openmp'
				version project.ext.libraryVersion
			}
		}
	}
}
